plugins {
	id 'jacoco'
	id 'com.github.kt3k.coveralls' version '2.6.3'
	id 'groovy'
	id 'maven-publish'
	id 'com.gradle.plugin-publish' version '0.9.7'
	id 'java-gradle-plugin'
}

group = 'com.lazan'
version = '0.5'

repositories {
	mavenCentral()
}

configurations {
	jacocoRuntime
}

dependencies {
	implementation 'org.apache.maven:maven-model:3.5.2'
	implementation 'org.apache.maven:maven-model-builder:3.5.2'
	testImplementation 'junit:junit:4.12'
	testImplementation 'org.mockito:mockito-all:1.10.19'
	testImplementation('org.spockframework:spock-core:2.1-groovy-3.0') {
        exclude module: 'groovy-all'
    }
	testRuntimeOnly files({project.tasks['createTestkitFiles']})
	jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

test {
	testLogging {
		exceptionFormat = 'full'
		showStandardStreams = true
	}
	beforeTest { TestDescriptor descriptor ->
		logger.lifecycle("Running test: $descriptor.name")
	}
}

task createTestkitFiles {
	def outputDir = file("$buildDir/testkit")

	inputs.files sourceSets.main.runtimeClasspath
	inputs.files configurations.jacocoRuntime
	outputs.dir outputDir

	doLast {
		String propString = "org.gradle.jvmargs=-javaagent:${configurations.jacocoRuntime.asPath}=destfile=$buildDir/jacoco/test.exec"
		mkdir(outputDir)
		file("$outputDir/testkit-gradle.properties").text = propString.replace('\\', '/')
	}
}

pluginBundle {
	website = 'https://github.com/uklance/gradle-dependency-export'
	vcsUrl = 'https://github.com/uklance/gradle-dependency-export.git'
	description = 'Export maven dependencies from a gradle project to the file system'
	tags = ['dependency', 'export', 'maven', 'offline', 'file', 'folder', 'directory']

	plugins {
		dependencyExport {
			id = 'com.lazan.dependency-export'
			displayName = 'Dependency Export Plugin'
		}
	}
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}
